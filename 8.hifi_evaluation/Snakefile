from glob import glob

cell_lines = list(set(list(map(lambda x: os.path.basename(x).split('.')[0], glob("normal_v2/msv/*msv")))))

cell_lines_sub = [cell for cell in list(set(list(map(lambda x: os.path.basename(x).split('.')[0], glob("normal_v2/msv/*msv"))))) if 'BL' not in cell]
print(cell_lines_sub)


rule phased_minisv_hg38_tg:
    input:
         msv = "normal_v2/msv/{cell_line}.hg38l+tg.c2s0.msv",
         hptag = "../1b.alignment_sv_tools_normal/output/extract_hp/{cell_line}_grch38.tsv.gz"
    output:
         "output/minisv_phase/{cell_line}_grch38_phased_tg.msv"
    conda: "minisvpy"
    shell:
         """
         zcat {input.hptag} | minisv annotatehp - {input.msv}  > {output}
         """

rule phased_minisv_hg38_tg_filter:
    input:
         msv = rules.phased_minisv_hg38_tg.output
    output:
         "output/minisv_phase/{cell_line}_grch38_phased_tg_filtered.msv"
    conda: "minisvpy"
    shell:
         """
         awk 'BEGIN{{IFS="\\t"}} {{split($NF, a, ","); if (!(a[2]>=1 && a[3]>=1)) {{print $0}}}}' {input} > {output}
         """


rule gafcall_eval_length:
    input:
        expand("normal_v2/msv/{{cell_line}}.{{comb}}.{count}.msv", count=['c3s0'])
               #count=['c4s2', 'c2s0', 'c3s0', 'c3s1'])
    output:
        eval_length="output/gafcall_eval/normal_v2/{cell_line}_{comb}_eval_len.tsv"
    shell:
         """
         for input in {input}; do
             if [[ $input =~ "chm13" ]]; then
                 ../gafcall/js/gafcall.js view -b ../4.gafcall_evaluation/chm13.reg.bed -c 5 -C -I $input >> {output.eval_length} 
             else
                 ../gafcall/js/gafcall.js view -b ../4.gafcall_evaluation/hg38.reg.bed -c 5 -C -I $input >> {output.eval_length} 
             fi
         done

         """

rule gafcall_eval_length_combine:
    input:
        eval_length = expand("output/gafcall_eval/normal_v2/{{cell_line}}_{comb}_eval_len.tsv", comb=['chm13l+x', 'chm13l+g', 'chm13l+gs', 'chm13l+s', 'hg38l+x', 'hg38l+t', 'hg38l+ts', 'hg38l+tg', 'hg38l+tgs'])
    output:
        eval_length = "output/gafcall_eval/normal_v2/{cell_line}_merged_eval.len.tsv"
    shell:
        "cat {input.eval_length} > {output.eval_length}"

rule gafcall_eval_length_merge_all:
    input:
        eval_length = expand("output/gafcall_eval/normal_v2/{cell_line}_merged_eval.len.tsv", cell_line=cell_lines)
    output:
        eval_length = "output/gafcall_eval/normal_v2/all_merged_eval.len.tsv"
    shell:
        "echo -e 'translocation\t>1M\t>100k\t>20k\tall_except_inv\tinv\tfile' > {output.eval_length} && cat {input.eval_length} >> {output.eval_length}"

rule plot_normal:
    input:
        eval_length = "output/gafcall_eval/normal_v2/all_merged_eval.len.tsv"
    params:
        select = 'BL'
    conda: "plot"
    output:
        paired = "output/gafcall_eval/normal_v2/paired_length_hifi.pdf",
        normal = "output/gafcall_eval/normal_v2/normal_length_hifi.pdf",
        hg38paired = "output/gafcall_eval/normal_v2/hg38_paired_length_hifi.pdf",
        chm13paired = "output/gafcall_eval/normal_v2/chm13_paired_length_hifi.pdf",
        hg38normal = "output/gafcall_eval/normal_v2/hg38_normal_length_hifi.pdf",
        chm13normal = "output/gafcall_eval/normal_v2/chm13_normal_length_hifi.pdf"
    script:
        "scripts/plot_sv_num_per_length.R"


rule colo829_hifi_evaluation:
    input:
        expand(os.path.join("../1a.alignment_sv_tools/output/minisv_eval_tnpair", '{cell_line}_hifi1_{assembly}_count{count}_eval.tsv'), cell_line=['COLO829'], count=[2,3,4,5,10], assembly=['chm13', 'grch38'])
    conda: "plot"
    output:
        table="colo829_hifi_clean.tsv",
        plot="colo829_hifi_clean.png",
        hg38plot="hg38_colo829_hifi_clean.png",
        chm13plot="chm13_colo829_hifi_clean.png",
        f1plot="colo829_hifi_clean_max.png",
    script:
        "scripts/tn_pair_precision_recall.R"


rule all: 
    input:
        "output/gafcall_eval/normal_v2/all_merged_eval.len.tsv",
        "output/gafcall_eval/normal_v2/normal_length_hifi.pdf",
        "colo829_hifi_clean.png",
        "hg38_colo829_hifi_clean.png", 
        "chm13_colo829_hifi_clean.png", 
        "output/gafcall_eval/normal_v2/hg38_paired_length_hifi.pdf",
        "output/gafcall_eval/normal_v2/chm13_paired_length_hifi.pdf",
        "output/gafcall_eval/normal_v2/hg38_normal_length_hifi.pdf",
        expand("output/minisv_phase/{cell_line}_grch38_phased_tg.msv", cell_line = cell_lines_sub),
        expand("output/minisv_phase/{cell_line}_grch38_phased_tg_filtered.msv", cell_line = cell_lines_sub)
    default_target: True
