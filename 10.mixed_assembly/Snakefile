import os
configfile: "config.yaml"

truth = {'chm13': '../2b.tumor_only_somatic_evaluation/output/truthset.colo829.out.chm13v2.crossmap_clean.vcf', 
         'grch38': '/homes6/hli/hli1/gafcall/COLO829.truth.hs38.vcf'}

def input_truth(wildcards):
    if wildcards.cell_line == 'COLO829_hifi1':
        return truth['grch38']
    else:
        #return ["../1a.alignment_sv_tools/output/severus/{cell_line}/grch38/somatic_SVs/severus_somatic.vcf", "../1a.alignment_sv_tools/output/savana/{cell_line}/grch38/grch38.classified.somatic.vcf", "../1a.alignment_sv_tools/output/nanomonsv/{cell_line}/grch38_tnpair.vcf"]
        return ["../1a.alignment_sv_tools/output/severus/{cell_line}/grch38/somatic_SVs/severus_somatic.vcf", "../1a.alignment_sv_tools/output/savana12/{cell_line}/grch38/grch38_T_tag.classified.somatic.vcf", "../1a.alignment_sv_tools/output/nanomonsv/{cell_line}/grch38_tnpair.vcf"]

rule gfa2fa:
    input:
         hap = "../1a.alignment_sv_tools/output/align/{cell_line}/chm13_mixdown.asm.bp.hap{hap}.p_ctg.gfa"
    output:
         "output/mixed_assembly/{cell_line}.asm.bp.hap{hap}.fa.gz"
    shell:
         """
         ../5.assembly_evaluation/gfatools/gfatools gfa2fa {input.hap} | bgzip -c - > {output}
         """


rule minimap2_on_assembly:
    input:
        hap = expand("output/mixed_assembly/{{cell_line}}.asm.bp.hap{hap}.fa.gz", hap=[1, 2]),
        fastq = "../1a.alignment_sv_tools/output/align/{cell_line}/chm13_mixdown.fastq.gz"
    output:
        paf = "output/align/{cell_line}_asm.paf.gz",
    resources:
        mem_mb=64000
    threads: 28
    shell:
        """
        ../1a.alignment_sv_tools/minimap2/minimap2 -t {threads} -cxmap-hifi -s50 <(zcat {input.hap}) -I100g --secondary=no {input.fastq} | gzip - > {output.paf}
        """


rule minimap2_on_defaultrefs:
    input:
        #../1a.alignment_sv_tools/chm13.fa.gz  ../1a.alignment_sv_tools/chm13.gfa.gz  ../1a.alignment_sv_tools/grch38.fa.gz
        fasta = "../1a.alignment_sv_tools/{assembly}.fa.gz",
        fastq = "../1a.alignment_sv_tools/output/align/{cell_line}/chm13_mixdown.fastq.gz"
    output:
        paf = "output/align/{cell_line}_{assembly}.paf.gz"
    resources:
        mem_mb=64000
    threads: 28
    shell:
        """
        ../1a.alignment_sv_tools/minimap2/minimap2 --ds -t {threads} -cxmap-hifi -s50 {input.fasta} --secondary=no {input.fastq} | gzip - > {output.paf}
        """


rule minigraph:
    input:
        assembly = "../1a.alignment_sv_tools/chm13.gfa.gz",
        fastq = "../1a.alignment_sv_tools/output/align/{cell_line}/chm13_mixdown.fastq.gz"
    output:
        paf = "output/align/{cell_line}_chm13g.paf.gz"
    threads: 28
    resources:
        mem_mb=64000
    shell:
        """
        ~/data/pangenome_sv_benchmarking/1a.alignment_sv_tools/minigraph/minigraph -cxlr -t {threads} {input.assembly} {input.fastq} | gzip - > {output.paf}
        """

rule minisv_mosaic_mixed_ltg_e:
    threads: 1
    resources:
        mem_mb=100000,
        run_time="8h"
    input:
        gaf = "output/align/{cell_line}_chm13g.paf.gz",
        paf = expand("output/align/{{cell_line}}_{assembly}.paf.gz", assembly=['chm13', 'grch38'])
    output:
        rsv = "output/minisv_mosaic/{cell_line}/grch38l_l+tg_mosaic.rsv"
    shell:
        """
        # always use chm13 graph genome as graph filter
        minisv.js e -n TUMOR -b grch38.cen-mask.bed {input.paf[1]} {input.paf[0]} {input.gaf} | bash > {output.rsv}
        """

rule minisv_mosaic_mixed_ltgs_e:
    threads: 1
    resources:
        mem_mb=100000,
        run_time="8h"
    input:
        asm = "output/align/{cell_line}_asm.paf.gz",
        gaf = "output/align/{cell_line}_chm13g.paf.gz",
        paf = expand("output/align/{{cell_line}}_{assembly}.paf.gz", assembly=['chm13', 'grch38'])
    output:
        rsv = "output/minisv_mosaic/{cell_line}/grch38l_l+t+g_mosaic.rsv"
        # NOTE: should be below
        #rsv = "output/minisv_mosaic/{cell_line}/grch38l_l+t+g+s_mosaic.rsv"
    shell:
        """
        # always use chm13 graph genome as graph filter
        minisv.js e -n TUMOR -0b grch38.cen-mask.bed {input.paf[1]} {input.paf[0]} {input.gaf} {input.asm} | bash > {output.rsv}
        """

rule minisv_call:
    input:
        rsv = rules.minisv_mosaic_mixed_ltgs_e.output.rsv
    output:
        msv = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g+s_mosaic.msv.gz"
    shell:
        """
        cat {input.rsv} | sort -k1,1 -k2,2 -S4g | minisv.js merge -c2 -s0 - | gzip - > {output.msv}
        """

use rule minisv_call as minisv_call_ltg with:
    input:
        rsv = "output/minisv_mosaic/{cell_line}/grch38l_l+tg_mosaic.rsv"
    output:
        msv = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g_mosaic.msv.gz"
   

for cutoff in [2,3,4,5,10]:
    rule:
        name: f"minisv_eval_mixed_count{cutoff}"
        input:
            sniffles_mosaic = "../1a.alignment_sv_tools/output/sniffles_mosaic/{cell_line}/grch38_mixdown_mosaic.vcf.gz",
            minisv_ltgs = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g+s_mosaic.msv.gz",
            minisv_ltg = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g_mosaic.msv.gz",
            truth = input_truth
        output:
            eval = f"output/minisv_mosaic_asm/{{cell_line}}_grch38_count{cutoff}_eval.tsv"
        params:
            c = cutoff
        shell: 
            """

            if [ {wildcards.cell_line} == 'COLO829_hifi1' ]; then
                minisv.js eval -c {params.c} -b ~/data/pangenome_sv_benchmarking/minisv/data/hs38.reg.bed {input.truth} {input.minisv_ltg} {input.minisv_ltgs} {input.sniffles_mosaic} > {output.eval}
            else
                for input in {input.sniffles_mosaic} {input.minisv_ltgs} {input.minisv_ltg}; do
                    minisv.js eval -M -c {params.c} -b ~/data/pangenome_sv_benchmarking/minisv/data/hs38.reg.bed $input {input.truth} | head -2 >> {output.eval}
                done
            fi


            """

    rule:
        name: f"minisv_eval_mixed_count{cutoff}_100kb"
        input:
            sniffles_mosaic = "../1a.alignment_sv_tools/output/sniffles_mosaic/{cell_line}/grch38_mixdown_mosaic.vcf.gz",
            minisv_ltgs = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g+s_mosaic.msv.gz",
            minisv_ltg = "output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g_mosaic.msv.gz",
            truth = input_truth
        output:
            eval = f"output/minisv_mosaic_asm/{{cell_line}}_grch38_count{cutoff}_eval_100kb.tsv"
        params:
            c = cutoff
        shell: 
            """

            if [ {wildcards.cell_line} == 'COLO829_hifi1' ]; then
                minisv.js eval -l 100000 -c {params.c} -b ~/data/pangenome_sv_benchmarking/minisv/data/hs38.reg.bed {input.truth} {input.minisv_ltg} {input.minisv_ltgs} {input.sniffles_mosaic} > {output.eval}
            else
                for input in {input.sniffles_mosaic} {input.minisv_ltgs} {input.minisv_ltg}; do
                    minisv.js eval -M -l 100000 -c {params.c} -b ~/data/pangenome_sv_benchmarking/minisv/data/hs38.reg.bed $input {input.truth} | head -2 >> {output.eval}
                done
            fi

            """

rule all:
    input:
        expand("output/mixed_assembly/{cell_line}.asm.bp.hap{hap}.fa.gz", cell_line = config['samples'], hap=[1, 2]),
        expand("output/align/{cell_line}_asm.paf.gz", cell_line = config['samples']),
        expand("output/align/{cell_line}_{assembly}.paf.gz", cell_line = config['samples'], assembly=['grch38', 'chm13']),
        expand("output/align/{cell_line}_chm13g.paf.gz", cell_line = config['samples']),
        expand("output/minisv_mosaic_asm/{cell_line}/grch38l_l+t+g+s_mosaic.msv.gz", cell_line = config['samples']),
        expand("output/minisv_mosaic_asm/{cell_line}_grch38_count{cutoff}_eval.tsv", cell_line = config['samples'], cutoff = [2,3,4,5,10]),
        expand("output/minisv_mosaic_asm/{cell_line}_grch38_count{cutoff}_eval_100kb.tsv", cell_line = config['samples'], cutoff = [2,3,4,5,10])
    default_target: True
